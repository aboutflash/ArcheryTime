import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import io.bit3.jsass.Compiler
import io.bit3.jsass.Options
import io.bit3.jsass.Output

import java.nio.charset.StandardCharsets
import java.nio.file.Files
import java.nio.file.Paths

group 'de.aboutflash.archerytime'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'de.aboutflash.archerytime.client.main.ArcheryTime'

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

configurations {
  all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  all*.exclude group: 'log4j', module: 'log4j'
}


tasks.withType(Exec) {
  doFirst {
    println commandLine
  }
}

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    google()
  }

  dependencies {
    classpath JSASS_DEP
    classpath SHADOW_DEP
    classpath ANDROID_DEP
  }
}

/**
 * Ensure default
 */
compileJava {
  options.encoding = "UTF-8"
}

dependencies {
  // Internal dependencies
  compile project(':common')

  // Utilities
  compile COMMONS_LANG3_DEP

  // For CLI parsing
  compile COMMONS_CLI_DEP

  // For logging
  compile REFLECTIONS_DEP

  // Used by LicenseService
  // compile HTTPCLIENT_DEP
  // compile JSON_DEP

  // TOOLING DEPENDENCIES
  testCompile HAMCREST_CORE_DEP
  testCompile HIBERNATE_VALIDATOR_DEP
  testCompile JUNIT_DEP
  testCompile LOG4J_OVER_SLF4J_DEP
  testCompile LOGBACK_CLASSIC_DEP
  testCompile MOCKITO_CORE_DEP
  testCompile OPENJFX_MONOCLE_DEP
  testCompile TESTFX_CORE_DEP
  testCompile TESTFX_JUNIT_DEP
  testCompile TRUTH_DEP
}


tasks.withType(Test) {
  environment JAVA_OPTS: "-Xms512m -Xmx4G"

  systemProperty 'quantum.verbose', 'false'
  systemProperty 'Dquantum.pulse', 'false'
  systemProperty 'javafx.verbose', 'false'
  systemProperty 'prism.verbose', 'false'

  systemProperty 'testfx.robot', 'glass'
  // See https://wiki.openjdk.java.net/display/OpenJFX/Monocle
  systemProperty 'glass.platform', 'Monocle'
  systemProperty 'monocle.platform', 'Headless'
  systemProperty 'prism.order', 'sw'

  systemProperty 'headless.geometry', '1920x1080'
  systemProperty 'com.sun.javafx.fontSize', '12'
  systemProperty 'com.sun.javafx.screenDPI', '96'

  systemProperty 'prism.debugfonts', 'false'
  systemProperty 'javafx.pulseLogger', 'false'
}

/**
 * Build one uber jar containing the complete spc client. Because we have to include
 * JAXB libraries into this code where each has an equally named configuration file into
 * their META-INF folder, we need to merge those files into one. That is the reason why
 * the shadow plugin was introduced for building the jar instead of the default jar plugin.
 *
 * And, besides that, it excludes all cryptographic signature files by default.
 */
task fatJar(type: ShadowJar, dependsOn: ['assemble', 'classes']) {
  archiveName = "archerytime.jar"

  manifest {
    attributes "Main-Class": "de.aboutflash.archerytime.client.main.ArcheryTime"
  }

  from sourceSets.main.output.classesDirs
  from sourceSets.main.output.resourcesDir
  from {
    project.configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }

  entryCompression ZipEntryCompression.DEFLATED
  exclude '**/*.log', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'

  mergeServiceFiles {
    path = 'META-INF/cxf'
  }
}

/**
 * Declare only the sources artifact for now.
 */
artifacts {
  archives sourceJar
}

/**
 * Compile scss to css.
 */

task compileSCSS {
  final URI inputFile = file("src/main/java/de/aboutflash/archerytime/client/scss/main.scss").toURI()
  final URI outputFile = file("src/main/generated/css/main.css").toURI()

  final Compiler compiler = new Compiler()
  final Options options = new Options()
  final Output output = compiler.compileFile(inputFile, outputFile, options)

  // Remove the charset declaration that confuses the JavaFX CSS parser.
  final String css = output.getCss().replace("@charset \"UTF-8\";", "")

  // Note: At this point, outputFile does not yet exist. So write it now...
  def target = Paths.get(outputFile)
  Files.createDirectories(target.parent)
  Files.write(target, css.getBytes(StandardCharsets.UTF_8))
}

task compileRemoteclientSCSS {
  final URI inputFile = file("src/main/java/de/aboutflash/archerytime/remoteclient/scss/display.scss").toURI()
  final URI outputFile = file("src/main/generated/css/display.css").toURI()

  final Compiler compiler = new Compiler()
  final Options options = new Options()
  final Output output = compiler.compileFile(inputFile, outputFile, options)

  // Remove the charset declaration that confuses the JavaFX CSS parser.
  final String css = output.getCss().replace("@charset \"UTF-8\";", "")

  // Note: At this point, outputFile does not yet exist. So write it now...
  def target = Paths.get(outputFile)
  Files.createDirectories(target.parent)
  Files.write(target, css.getBytes(StandardCharsets.UTF_8))
}

/**
 * compass watcher task.
 *
 * Start compassWatch with:
 * `./gradlew compassWatch -t`
 */
task compassWatch {
  doLast {
    compileSCSS
  }
}

compassWatch {
  inputs.files fileTree(dir: 'src/main/java', include: '**/*.scss')
}

assemble.doLast {
  compileSCSS
}
